0 REM MINOTAUR - bugged
1 PRINT"{clear}";:POKE53280,14:POKE53281,13:PRINTTAB(10);"{black}minotaur master-mind":
2 PRINT:PRINTTAB(30);"by a.lacey":PRINT
4 PRINT:PRINT"level of difficulty?(1-9)";
5 GETX$:IFX$=""THEN5
6 IFVAL(X$)<1ORVAL(X$)>9THEN5
7 LD%=VAL(X$):PRINTLD%
8 FORT=1TO1000:NEXT
10 GOTO68
20 FORI=1225TO1785STEP40
22 FORJ=0TO37
24 K1=INT(RND(0)*13)
26 IFK1>9THEN30
27 POKEI+J,K1+48:POKEI+J+54272,2
28 GOTO40
30 IFK1<>10THEN33
31 POKEI+J,24:POKEI+J+54272,3
32 GOTO40
33 IFK1<>11THEN35
34 POKEI+J,63:POKEI+J+54272,5:GOTO40
35 POKEI+J,42:POKEI+J+54272,7
40 NEXT:NEXT
42 POKE1244,160:POKE55516,1
45 RETURN
68 R%=1:SN%=INT(RND(0)*999+1)
69 D1%=INT(SN%/100):D2%=INT((SN%-D1%*100)/10):D3%=SN%-D1%*100-D2%*10
70 PRINT"{clear}";:POKE53280,0:POKE53281,0:GOSUB20
72 PP%=1804:M%=1524:IFPEEK(1804)>48THENLS%=PEEK(1804)-48
73 IFPEEK(1804)=63ORPEEK(1804)=42ORPEEK(1804)=24THENLS%=0:POKE1804,48
74 TS%=TS%+LS%:CU=PEEK(1524):POKE1524,13:POKE55796,7:POKE1244,160:POKE55516,1
75 PRINT"{cyan}{reverse on}round #";R%;"{reverse off} {reverse on}level of diff. ";LD%;"{reverse off}";TAB(30);"{reverse on}player 1{reverse off}"
76 FORI=1TO21:PRINT:NEXT:PRINT"{cyan} total score ";TS%;TAB(21);"last score";LS%;
80 C=0:Q=1
81 GETX$:C=C+1
82 IFC=7THENQ=1-Q:C=0
83 IFQ<>0THEN86
84 IFPEEK(PP%)>63THENPOKEPP%,PEEK(PP%)-128:Q=1:GOTO86
85 POKEPP%,PEEK(PP%)+128:Q=1
86 IFX$=""THEN81
87 GOSUB480:IFPEEK(PP%)>64THENPOKEPP%,PEEK(PP%)-128
89 IFX$="p"THEN100
90 IFX$="o"THEN110
92 IFX$="@"THEN120
93 IFX$=":"THEN130
94 IFX$="+"THEN140
95 IFX$="0"THEN150
96 IFX$="l"THEN160
97 IFX$=";"THEN170
98 IFX$="t"THEN178
99 GOTO81
100 IFPEEK(PP%-40)=32THEN81
102 LS%=PEEK(PP%)-48
104 POKEPP%,32:PP%=PP%-40
105 GOTO180
110 IFPEEK(PP%-1)=32THEN81
112 LS%=PEEK(PP%)-48
114 POKEPP%,32:PP%=PP%-1
115 GOTO180
120 IFPEEK(PP%+1)=32THEN81
122 LS%=PEEK(PP%)-48
124 POKEPP%,32:PP%=PP%+1
125 GOTO180
130 IFPEEK(PP%+40)=32THEN81
132 LS%=PEEK(PP%)-48
134 POKEPP%,32:PP%=PP%+40
135 GOTO180
140 IFPEEK(PP%-39)=32THEN81
142 LS%=PEEK(PP%)-48
144 POKEPP%,32:PP%=PP%-39
145 GOTO180
150 IFPEEK(PP%-41)=32THEN81
152 LS%=PEEK(PP%)-48
154 POKEPP%,32:PP%=PP%-41
155 GOTO180
160 IFPEEK(PP%+39)=32THEN81
162 LS%=PEEK(PP%)-48
164 POKEPP%,32:PP%=PP%+39
165 GOTO180
170 IFPEEK(PP%+41)=32THEN81
172 LS%=PEEK(PP%)-48
174 POKEPP%,32:PP%=PP%+41
175 GOTO180
178 PR%=INT((PP%-1024)/40):PC%=PP%-PR%*40-1024:GOSUB360:IFPP%<>M%THEN178
179 GOTO400
180 PR%=INT((PP%-1024)/40):PC%=PP%-PR%*40-1024
181 K1=PEEK(PP%):IFK1=63THENGOSUB200
182 IFPP%=M%THEN400
183 IFK1=24THENGOSUB210
184 IFK1=42THENGOSUB300
186 IFK1=160THEN320
190 K1=PEEK(PP%)-48:IFLS%+K1>=10-LD%THENGOSUB360
192 TS%=TS%+K1:GOSUB450
194 IFPP%=M%THEN400
196 GOTO80
200 I=INT(RND(1)*10):POKEPP%,I+48:POKEPP%+54272,2
202 RETURN
210 POKEPP%,48
211 IFNC%>=9THENGOTO295
213 K2=INT(RND(0)*9+1):IFT%(K2)=1THEN213
214 T%(K2)=1:NC%=NC%+1
216 ONK2GOTO220,225,230,235,240,245,250,255,260
220 GOSUB460:PRINT"sum of digits of secret":PRINTTAB(9);"number equals ";
222 PRINTD1%+D2%+D3%
224 GOSUB470:RETURN
225 GOSUB460:PRINT"secret number is ";
226 IFSN%/2=INT(SN%/2)THENPRINT"even":GOTO228
227 PRINT"odd"
228 PRINT:GOSUB470:RETURN
230 K=INT(RND(1)*200)+SN%:J=K-200:IFJ<0THENJ=0
232 IFK>1000THENK=1000
233 GOSUB460:PRINT"number is between";J:PRINTTAB(9);"and";K
234 GOSUB470:RETURN
235 GOSUB460:PRINT"number is ";
236 IFSN%/5=INT(SN%/5)THENPRINT"divisible":GOTO238
237 PRINT"not divisible"
238 PRINTTAB(9);"by 5":GOSUB470:RETURN
240 L=D1%:IFD2%>LTHENL=D2%
241 IFD3%>LTHENL=D3%
242 GOSUB460:PRINT"largest digit in the":PRINTTAB(9);"secret number is";L
243 GOSUB470:RETURN
245 GOSUB460:PRINT"first digit in the secret":PRINTTAB(9);"number is ";
246 IFD1%>5THENPRINT"larger than 5":GOTO249
247 IFD1%<5THENPRINT"less than 5":GOTO249
248 PRINT"equal to 5"
249 GOSUB470:RETURN
250 GOSUB460:PRINT"sum of first and third":PRINTTAB(9);"digits is ";
252 PRINTD1%+D3%
253 GOSUB470:RETURN
255 GOSUB460:PRINT"one of he digits in the":PRINTTAB(9);"secret number is ";
256 K=INT(RND(1)*3):IFK=0ANDSN%>99THENPRINTD1%:GOTO259
257 IFK=1ANDSN%>9THENPRINTD2%:GOTO259
258 PRINTD3%
259 GOSUB470:RETURN
260 GOSUB460:PRINT"product of the non-zero":PRINTTAB(9);"digits is";
261 IFD1%=0ANDD2%=0THENPRINTD3%:GOTO268
262 IFD1%=0ANDD3%=0THENPRINTD2%:GOTO268
263 IFD1%=0THENPRINTD2%*D3%:GOTO268
264 IFD2%=0ANDD3%=0THENPRINTD1%:GOTO268
265 IFD2%=0THENPRINTD1%*D3%:GOTO268
266 IFD3%=0THENPRINTD1%*D2%:GOTO268
267 PRINTD1%*D2%*D3%
268 GOSUB470:RETURN
295 GOTO230
300 POKEPP%,48
302 IFTS%<11THENRETURN
304 I=INT(RND(0)*100):IFI>50THENTS%=TS%-10:RETURN
306 IFI<10THENTS%=TS%+50:RETURN
310 TS%=TS%+10:RETURN
320 GOSUB460:PRINT"guess at the secret number":PRINTTAB(9);
322 INPUT"and hit {reverse on}return{reverse off}";K$
324 I=VAL(K$):IFI=SN%THEN400
325 G1%=INT(I/100):G2%=INT((I-G1%*100)/10):G3%=I-G1%*100-G2%*10
326 PRINT"{clear}":PRINT:IFD1%=G1%THENPRINT"first digit correct":PRINT
328 IFD2%=G2%THENPRINT"second digit correct":PRINT
330 IFD3%=G3%THENPRINT"third digit correct"
332 IFD1%<>G1%ANDD2%<>G2%ANDD3%<>G3%THENPRINT"no digits correct"
334 FORT=1TO2000:NEXT
336 R%=R%+1:GOTO70
360 MR%=INT((M%-1024)/40):MC%=M%-MR%*40-1024
362 IFMC%<>PC%THEN370
364 IFMR%>PR%THENMR%=MR%-1:GOTO390
366 MR%=MR%+1:GOTO390
370 IFMR%<>PR%THEN376
372 IFMC%>PC%THENMC%=MC%-1:GOTO390
374 MC%=MC%+1:GOTO390
376 IFM%>PP%THEN382
378 IFMC%<PC%THENMC%=MC%+1:GOTO390
380 MC%=MC%-1:GOTO390
382 MR%=MR%-1
390 K=1024+40*MR%+MC%:IFPEEK(K)=160THENRETURN
391 POKEM%,CU:IFCU=63THENPOKEM%+54272,5:GOTO395
392 IFCU>=48THENPOKEM%+54272,2:GOTO395
393 IFCU=24THENPOKEM%+54272,3:GOTO395
394 POKEM%+54272,7
395 M%=K:CU=PEEK(M%)
396 POKEM%,13:POKEM%+54272,7
397 K=54272:FORL=KTOK+24:POKEL,0:NEXT:POKEK+5,96:POKEK+24,15:POKEK+4,33
398 POKEK,209:POKEK+1,18:FORT=1TO30:NEXT:POKEK,0:POKEK+1,0
399 RETURN
400 PRINT"{clear}";:IFM%=PP%THEN420
405 FORI=1TO18:PRINT"{space*3}congratulations!{space*3}congratulations!":NEXT:PRINT
410 PRINT"you have defeated the minotaur!!":PRINT"your score is ";TS%;
415 PRINT"in ";R%;" rounds":GOTO440
420 FORI=1TO18:PRINT"{space*4}minotaur wins!{space*4}minotaur wins!":NEXT:PRINT
425 PRINT"the secret number was ";SN%
440 PRINT:PRINT"hit any key for another game"
445 GETX$:IFX$=""THEN445
446 RUN
450 PRINTCHR$(13);CHR$(145);" total score{space*9}";TAB(21);"last score{space*5}";
452 PRINTCHR$(13);CHR$(145);" total score ";TS%;TAB(21);"last score";LS%;
455 RETURN
460 PRINTCHR$(13);:FORI=1TO21:PRINTCHR$(145);:NEXT:PRINT"{yellow}";
462 PRINTTAB(9);:RETURN
470 FORI=1TO18:PRINTCHR$(17);:NEXT
472 PRINTTAB(35);"{cyan}";
474 RETURN
480 FORI=1112TO1138:POKEI,32:NEXT
482 FORI=1152TO1178:POKEI,32:NEXT
485 RETURN
2000 K1=INT(RND(0)*14):PRINTK1,:GOTO2000
